
&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	ОбновитьДанныеКурсов();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ДатаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеКурсов()
	
	Если ЗначениеЗаполнено(ДатаКурсов) Тогда
		
		ТаблицаКурсов.Очистить();
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	Валюты.Ссылка КАК Валюта
		|ПОМЕСТИТЬ ВТ_Валюты
		|ИЗ
		|	Справочник.Валюты КАК Валюты
		|ГДЕ
		|	Валюты.Ссылка <> ЗНАЧЕНИЕ(Справочник.Валюты.РоссийскийРубль)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Валюты.Валюта КАК Валюта,
		|	КурсыВалют.Курс КАК Курс,
		|	КурсыВалют.Кратность КАК Кратность
		|ИЗ
		|	ВТ_Валюты КАК ВТ_Валюты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО ВТ_Валюты.Валюта = КурсыВалют.Валюта
		|			И (КурсыВалют.Период = &Период)");
		
		Запрос.УстановитьПараметр("Период", ДатаКурсов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаКурсов.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеКурсовНаСервере()
	
	Для Каждого СтрокаТЧ Из ТаблицаКурсов Цикл
		
		Если ЗначениеЗаполнено(СтрокаТЧ.Курс) Тогда
			
			Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТЧ, "Валюта, Курс, Кратность");
			Запись.Период = ДатаКурсов;
			Запись.Записать(Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныеКурсов(Команда)
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьДанныеКурсовЗавершение", ЭтотОбъект), "Записать данные курсов валют?", РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДанныеКурсовЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаписатьДанныеКурсовНаСервере();
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Дата") Тогда
		ДатаКурсов = Параметры.Дата;
		ОбновитьДанныеКурсов();
	КонецЕсли;
	
КонецПроцедуры

