
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФинансовыйПланПланирование.Ссылка КАК ФинансовыйПлан,
	|	ФинансовыйПланПланирование.НомерЗаявки КАК НомерЗаявки,
	|	ФинансовыйПланПланирование.IDЗаявки КАК IDЗаявки,
	|	ФинансовыйПланПланирование.Сумма КАК Сумма,
	|	ФинансовыйПланПланирование.СуммаНДС КАК СуммаНДС,
	|	ФинансовыйПланПланирование.ВалютнаяСумма КАК ВалютнаяСумма,
	|	ФинансовыйПланПланирование.Валюта КАК Валюта,
	|	ФинансовыйПланПланирование.ВалютаЗаявки КАК ВалютаЗаявки,
	|	ФинансовыйПланПланирование.Контрагент КАК Контрагент,
	|	ФинансовыйПланПланирование.Договор КАК Договор,
	|	ФинансовыйПланПланирование.GUIDСтрокиФинансовогоПлана КАК GUIDСтрокиФинансовогоПлана,
	|	ФинансовыйПланПланирование.Ссылка.Номер КАК Номер,
	|	ФинансовыйПланПланирование.Ссылка.Дата КАК Дата,
	|	ФинансовыйПланПланирование.КатегорияОперации КАК КатегорияОперации
	|ИЗ
	|	Документ.ФинансовыйПлан.Планирование КАК ФинансовыйПланПланирование
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ФинансовыйПланПланирование.Ссылка.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&ДатаАнализа, ДЕНЬ)
	|	И НЕ ФинансовыйПланПланирование.Ссылка.ПометкаУдаления
	|	И ФинансовыйПланПланирование.Организация = &ОрганизацияАнализа
	|	И ФинансовыйПланПланирование.НомерЗаявки <> """"
	|	И НЕ ФинансовыйПланПланирование.НомерЗаявки В (&ИспользуемыеЗаявки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФинансовыйПланПланирование.Ссылка.Дата
	|ИТОГИ ПО
	|	ФинансовыйПлан";
	
	Запрос.УстановитьПараметр("ДатаАнализа", Параметры.ДатаАнализа);
	Запрос.УстановитьПараметр("ОрганизацияАнализа", Параметры.ОрганизацияАнализа);
	Запрос.УстановитьПараметр("ИспользуемыеЗаявки", Параметры.ИспользуемыеЗаявки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСсылка.Следующий() Цикл
		
		НоваяСтрокаФинансовыеПланы = ТаблицаФинансовыхПланов.Добавить();
		НоваяСтрокаФинансовыеПланы.GUIDФинансовогоПлана = СокрЛП(ВыборкаСсылка.ФинансовыйПлан.УникальныйИдентификатор());
		
		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
		ВПервыйРаз = Истина;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если ВПервыйРаз Тогда
				ВПервыйРаз = Ложь;
				НоваяСтрокаФинансовыеПланы.Номер = ВыборкаДетальныеЗаписи.Номер;
				НоваяСтрокаФинансовыеПланы.Дата = ВыборкаДетальныеЗаписи.Дата;
			КонецЕсли;
			
			НоваяСтрокаЗаявок = ТаблицаЗаявокХранение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗаявок, ВыборкаДетальныеЗаписи);
			НоваяСтрокаЗаявок.GUIDСтрокиФинансовогоПлана = ВыборкаДетальныеЗаписи.GUIDСтрокиФинансовогоПлана;	
			НоваяСтрокаЗаявок.GUIDФинансовогоПлана = НоваяСтрокаФинансовыеПланы.GUIDФинансовогоПлана;	
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФинансовыхПлановПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаФинансовыхПланов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТаблицаЗаявок.Очистить();
		Для Каждого СтрокаЗаявок Из ТаблицаЗаявокХранение.НайтиСтроки(Новый Структура("GUIDФинансовогоПлана", ТекущиеДанные.GUIDФинансовогоПлана)) Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаЗаявок.Добавить(), СтрокаЗаявок);	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаЗаявок.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Закрыть(Новый Структура("НомерЗаявки, IDЗаявки, GUIDСтрокиФинансовогоПлана, КатегорияОперации", ТекущиеДанные.НомерЗаявки, ТекущиеДанные.IDЗаявки, ТекущиеДанные.GUIDСтрокиФинансовогоПлана, ТекущиеДанные.КатегорияОперации));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаявокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаЗаявок.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Закрыть(Новый Структура("НомерЗаявки, IDЗаявки, GUIDСтрокиФинансовогоПлана, КатегорияОперации", ТекущиеДанные.НомерЗаявки, ТекущиеДанные.IDЗаявки, ТекущиеДанные.GUIDСтрокиФинансовогоПлана, ТекущиеДанные.КатегорияОперации));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ТаблицаЗаявокХранение.Количество() = 0 Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект), "Нет заявок на эту дату!");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат) Экспорт
	Закрыть();
КонецПроцедуры
