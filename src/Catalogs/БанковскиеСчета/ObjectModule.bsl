
Процедура ПередЗаписью(Отказ)
	
	ВалютаРубли = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Если НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда			
		ВалютаДенежныхСредств = ВалютаРубли;
		Валютный = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидСчета) Тогда			
		ВидСчета = Перечисления.ВидыБанковскихСчетов.Текущий;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		
		ПредставлениеВалюты = "" + ВалютаДенежныхСредств;
		
		ПредставлениеБанка = "";
		
		Если ЗначениеЗаполнено(Банк) Тогда
			ПредставлениеБанка = СокрЛП(Банк.Наименование);
		КонецЕсли;
		
		ПредставлениеВидаСчета = СокрЛП(ВидСчета);
		
		Если ВалютаДенежныхСредств = ВалютаРубли Тогда
			
			Если ВидСчета = Перечисления.ВидыБанковскихСчетов.Расчетный Тогда
				Наименование = "" + ПредставлениеБанка + " (Рублевый)";
			Иначе
				Наименование = "" + ПредставлениеБанка + " (" + ПредставлениеВидаСчета + ")";
			КонецЕсли;
			
		Иначе
			Наименование = "" + ПредставлениеБанка + " ("  + ПредставлениеВидаСчета + " " + ПредставлениеВалюты + ")";
			
		КонецЕсли;
			
	КонецЕсли;
	
	ТипВладельцаОрганизация = ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации");
	
	// Поиск парного счета
	
	Если НЕ ЗначениеЗаполнено(ПарныйБанковскийСчет) Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	БанковскиеСчета.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.НомерСчета = &НомерСчета
		|	И БанковскиеСчета.Банк = &Банк
		|	И ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(БанковскиеСчета.Владелец) = ТИП(Справочник.Организации)
		|				ТОГДА БанковскиеСчета.Владелец.ВнутреннийКонтрагент = &Владелец
		|			КОГДА ТИПЗНАЧЕНИЯ(БанковскиеСчета.Владелец) = ТИП(Справочник.Контрагенты)
		|				ТОГДА БанковскиеСчета.Владелец.ВнутренняяОрганизация = &Владелец
		|		КОНЕЦ");
		
		Запрос.УстановитьПараметр("Владелец", Владелец);
		Запрос.УстановитьПараметр("НомерСчета", СокрЛП(НомерСчета));
		Запрос.УстановитьПараметр("Банк", Банк);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПарныйБанковскийСчет = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
		
	
КонецПроцедуры
