
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяМетода", "РаботаСКурсамиВалют.ЗагрузитьАктуальныйКурс");
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Завершено);
	Отбор.Вставить("Начало", ДобавитьМесяц(ТекущаяДата(), -1));
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТаблицаФоновыхЗаданий.Количество() > 0 Тогда
		
		ДатаУдачнойЗагрузки = ТаблицаФоновыхЗаданий[0].Конец;
		ТекстСообщения = "Последняя удачная загрузка " + ДатаУдачнойЗагрузки;
		ЦветСообщения = ?(НачалоДня(ДатаУдачнойЗагрузки) = НачалоДня(ТекущаяДата()), WebЦвета.ЦветМорскойВолны, WebЦвета.Коричневый);
		Элементы.ИнформацияОЗагрузкеКурса.Заголовок = Новый ФорматированнаяСтрока(ТекстСообщения,, ЦветСообщения);
		
	Иначе
		Элементы.ИнформацияОЗагрузкеКурса.Заголовок = "Нет данных об удачных загрузках курса!";
	КонецЕсли;
	
	ДатаКурса = ТекущаяДата();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПериодСреза", ДатаКурса);
	
	Если НЕ РольДоступна("ДобавлениеИзменениеКурсовВалют")
		И НЕ РольДоступна("ПолныеПрава") Тогда
		Элементы.ЗагрузитьКурсыВалют.Видимость = Ложь;
		Элементы.ОткрытьФормуРучногоВвода.Видимость = Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	КурсыВалют.Период КАК Период
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалют
	|ГДЕ
	|	КурсыВалют.Период = &Период");
	
	Запрос.УстановитьПараметр("Период", НачалоДня(ТекущаяДата()));
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Элементы.ИнформацияОбАктуальностиКурса.Заголовок = Новый ФорматированнаяСтрока("Курсы валют актуальны!",, WebЦвета.ЦветМорскойВолны);
	Иначе
		Элементы.ИнформацияОбАктуальностиКурса.Заголовок = Новый ФорматированнаяСтрока("Курсы валют неактуальны!",, WebЦвета.Коричневый);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаКурсаПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПериодСреза", ДатаКурса);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКурсыВалют(Команда)
	
	ИмяФормыЗагрузки = "Обработка.ЗагрузкаКурсовВалют.Форма";
	ПараметрыФормы = Новый Структура("ОткрытиеИзСписка");
	ОткрытьФорму(ИмяФормыЗагрузки, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРучногоВвода(Команда)
	ОткрытьФорму("РегистрСведений.КурсыВалют.Форма.ФормаРучногоВводаКурса", Новый Структура("Дата", ДатаКурса), ЭтотОбъект,,,, Новый ОписаниеОповещения("ОткрытьФормуРучногоВводаЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРучногоВводаЗавершение(Результат, Параметры) Экспорт
	Элементы.Список.Обновить();	
КонецПроцедуры
