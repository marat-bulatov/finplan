
&НаКлиенте
Процедура УправленческийУчетАлгоритмВычисленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.УправленческийУчет.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОткрытьФорму("Справочник.ДанныеАвтозаполненияПлатежей.Форма.ФормаРедактированияАлгоритма", 
						Новый Структура("АлгоритмВычисления", ТекущиеДанные.АлгоритмВычисления),
						ЭтотОбъект,,,,
						Новый ОписаниеОповещения("УправленческийУчетАлгоритмВычисленияНачалоВыбораЗавершение", ЭтотОбъект),
						РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправленческийУчетАлгоритмВычисленияНачалоВыбораЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Элементы.УправленческийУчет.ТекущиеДанные.АлгоритмВычисления = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обработать(Команда)
	
	Объект.ШаблонНазначенияПлатежа = СокрЛП(Объект.ШаблонНазначенияПлатежа);
	Объект.ШаблонНазначенияПлатежа = нрег(Объект.ШаблонНазначенияПлатежа);
	Объект.ШаблонНазначенияПлатежа = СтрЗаменить(Объект.ШаблонНазначенияПлатежа, "  ", " ");
	Объект.ШаблонНазначенияПлатежа = СтрЗаменить(Объект.ШаблонНазначенияПлатежа, "\", "\\");
	Объект.ШаблонНазначенияПлатежа = СтрЗаменить(Объект.ШаблонНазначенияПлатежа, "/", "\/");
	Объект.ШаблонНазначенияПлатежа = СтрЗаменить(Объект.ШаблонНазначенияПлатежа, "(", "\(");
	Объект.ШаблонНазначенияПлатежа = СтрЗаменить(Объект.ШаблонНазначенияПлатежа, ")", "\)");
	Объект.ШаблонНазначенияПлатежа = СтрЗаменить(Объект.ШаблонНазначенияПлатежа, " ", "\s+");
	
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмВычисленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФорму("Справочник.ДанныеАвтозаполненияПлатежей.Форма.ФормаРедактированияАлгоритма", 
						Новый Структура("АлгоритмВычисления", Объект.АлгоритмВычисления),
						ЭтотОбъект,,,,
						Новый ОписаниеОповещения("АлгоритмВычисленияНачалоВыбораЗавершение", ЭтотОбъект),
						РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
					
КонецПроцедуры
					
&НаКлиенте
Процедура АлгоритмВычисленияНачалоВыбораЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Объект.АлгоритмВычисления = Результат;
	КонецЕсли;
	
КонецПроцедуры					

&НаСервере
Процедура КатегорияОперацииПриИзмененииНаСервере()
	
	Объект.УправленческийУчет.Очистить();
	
	Если ЗначениеЗаполнено(Объект.КатегорияОперации) Тогда
		
		Для Каждого СтруктураДанных Из УправленческийУчетСервер.ПодготовитьДанныеУправленческогоУчета(Объект.КатегорияОперации) Цикл
			
			НоваяСтрока = Объект.УправленческийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураДанных,, "Значение");
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияОперацииПриИзменении(Элемент)
	КатегорияОперацииПриИзмененииНаСервере();
КонецПроцедуры


