
&НаСервере
Процедура СтартНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Контрагенты.ИНН КАК ИНН,
	|	КОЛИЧЕСТВО(Контрагенты.ИНН) КАК Количество
	|ПОМЕСТИТЬ ВТ_ИНН
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН <> """"
	|	И НЕ Контрагенты.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	Контрагенты.ИНН
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(Контрагенты.ИНН) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИНН.ИНН КАК ИНН,
	|	Контрагенты.Ссылка КАК Контрагент,
	|	Контрагенты.ВидКонтрагента КАК ВидКонтрагента
	|ИЗ
	|	ВТ_ИНН КАК ВТ_ИНН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ВТ_ИНН.ИНН = Контрагенты.ИНН
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИНН
	|ИТОГИ ПО
	|	ИНН";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЭлементыДерева = ДеревоКонтрагентов.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	ВыборкаПоИНН = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоИНН.Следующий() Цикл
		
		ЭлементыДерева = ДеревоКонтрагентов.ПолучитьЭлементы();
		ЭлементПоИНН = ЭлементыДерева.Добавить();
		ЭлементПоИНН.ИНН = СокрЛП(ВыборкаПоИНН.ИНН);
		
		ВыборкаПоИнформацииПоИНН = РегистрыСведений.ИнформацияПоИНН.Выбрать(Новый Структура("ИНН", ВыборкаПоИНН.ИНН));
		
		Если НЕ ВыборкаПоИнформацииПоИНН.Следующий() Тогда
			
			Если СтрДлина(ЭлементПоИНН.ИНН) = 10 Тогда
				РеквизитыКонтрагента = РаботаСКонтрагентами.РеквизитыЮридическогоЛицаПоИНН(ВыборкаПоИНН.ИНН);
			Иначе
				РеквизитыКонтрагента = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ВыборкаПоИНН.ИНН);
			КонецЕсли;
			
			Запись = РегистрыСведений.ИнформацияПоИНН.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, РеквизитыКонтрагента);
			Запись.Записать();
			
			ЭлементПоИНН.Информация = "" + РеквизитыКонтрагента.НаименованиеСокращенное + " / " + РеквизитыКонтрагента.НаименованиеПолное;
			
		Иначе
			
			ЭлементПоИНН.Информация = "" + ВыборкаПоИнформацииПоИНН.НаименованиеСокращенное + " / " + ВыборкаПоИнформацииПоИНН.НаименованиеПолное;
			
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаПоИНН.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ЭлементыДерева = ЭлементПоИНН.ПолучитьЭлементы();
			НоваяСтрока = ЭлементыДерева.Добавить();
			НоваяСтрока.ИНН = ВыборкаДетальныеЗаписи.ИНН;
			НоваяСтрока.Контрагент = ВыборкаДетальныеЗаписи.Контрагент;
			
		КонецЦикла;
		
	КонецЦикла;

	
КонецПроцедуры

&НаКлиенте
Процедура Старт(Команда)
	СтартНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СтартНаСервере();
КонецПроцедуры
